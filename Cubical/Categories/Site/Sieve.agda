{-# OPTIONS --safe #-}
module Cubical.Categories.Site.Sieve where

-- Definition of sieves on an object
-- and the sieve generated by a cover.

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Function
open import Cubical.Foundations.HLevels
open import Cubical.Foundations.Structure

open import Cubical.Functions.Logic using (∀[]-syntax; ∀[∶]-syntax; _⇒_; _⇔_)

open import Cubical.HITs.PropositionalTruncation as PT

open import Cubical.Data.Sigma

open import Cubical.Categories.Category
open import Cubical.Categories.Site.Cover

module _
  {ℓ ℓ' : Level}
  (C : Category ℓ ℓ')
  where

  open Category C hiding (_∘_)

  record Sieve (ℓsie : Level) (c : ob) : Type (ℓ-max ℓ (ℓ-max ℓ' (ℓ-suc ℓsie))) where
    no-eta-equality
    field
      passes : {d : ob} → Hom[ d , c ] → hProp ℓsie
      closedUnderPrecomposition :
        {d' d : ob} (p : Hom[ d' , d ]) (f : Hom[ d , c ]) →
        ⟨ passes f ⟩ → ⟨ passes (p ⋆ f) ⟩

  open Sieve

  sieveRefinesSieve :
    {ℓS' ℓS : Level} →
    {c : ob} →
    Sieve ℓS' c →
    Sieve ℓS c →
    hProp (ℓ-max (ℓ-max (ℓ-max ℓ ℓ') ℓS') ℓS)
  sieveRefinesSieve S' S =
    ∀[ d ∶ ob ] ∀[ f ∶ Hom[ d , _ ] ] (passes S' f ⇒ passes S f)

  generatedSieve : {ℓ : Level} {c : ob} → Cover C ℓ c → Sieve (ℓ-max ℓ' ℓ) c
  passes (generatedSieve cov) f =
      (∃[ i ∈ ⟨ cov ⟩ ] Σ[ h ∈ Hom[ _ , _ ] ] f ≡ h ⋆ patchArr C cov i)
    , isPropPropTrunc
  closedUnderPrecomposition (generatedSieve cov) p f =
    PT.rec isPropPropTrunc λ (i , h , f≡hi) →
      ∣   i
        , (p ⋆ h)
        , ( (p ⋆ f)                      ≡⟨ cong (p ⋆_) f≡hi ⟩
            (p ⋆ (h ⋆ patchArr C cov i)) ≡⟨ sym (⋆Assoc p h (patchArr C cov i)) ⟩
            ((p ⋆ h) ⋆ patchArr C cov i) ∎ )
      ∣₁

  coverRefinesSieve :
    {ℓcov ℓsie : Level}
    {c : ob} →
    Cover C ℓcov c →
    Sieve ℓsie c →
    hProp (ℓ-max ℓcov ℓsie)
  coverRefinesSieve cov S =
    ∀[ i ] passes S (patchArr C cov i)

  coverRefinesGeneratedSieve :
    {ℓ : Level} →
    {c : ob} →
    {cov : Cover C ℓ c} →
    ⟨ coverRefinesSieve cov (generatedSieve cov) ⟩
  coverRefinesGeneratedSieve i = ∣ i , id , sym (⋆IdL _) ∣₁

  -- The universal property of generatedSieve
  generatedSieveIsUniversal :
    {ℓcov ℓsie : Level} →
    {c : ob} →
    (cov : Cover C ℓcov c) →
    (S : Sieve ℓsie c) →
    ⟨ coverRefinesSieve cov S ⟩ →
    ⟨ sieveRefinesSieve (generatedSieve cov) S ⟩
  generatedSieveIsUniversal cov S cov≤S d f =
    PT.rec (str (passes S f)) (λ (i , g , eq) →
      subst (⟨_⟩ ∘ passes S) (sym eq)
        (closedUnderPrecomposition S g (patchArr C cov i) (cov≤S i)) )

  pulledBackSieve :
    {ℓsie : Level} →
    {c d : ob} →
    (Hom[ c , d ]) →
    Sieve ℓsie d →
    Sieve ℓsie c
  passes (pulledBackSieve f S) g = passes S (g ⋆ f)
  closedUnderPrecomposition (pulledBackSieve f S) p g pass =
    subst (⟨_⟩ ∘ passes S) (sym (⋆Assoc p g f))
      (closedUnderPrecomposition S p (g ⋆ f) pass)
